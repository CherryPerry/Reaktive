import com.badoo.reaktive.configuration.Target

/*
 * In order to build this module for LinuxX64 you should install libcurl4-openssl-dev package in your system
 */
plugins {
    id 'mpp-configuration'
    id 'detekt-configuration'
}

configuration {
    enableLinuxArm32Hfp()
}

kotlin {
    targets {
//        fromPreset(presets.iosX64, 'darwinCommon')
        if (Target.shouldDefineTarget(project, Target.LINUX)) {
            linuxX64 {
                compilations.main.cinterops {
                    libcurl.includeDirs.headerFilterOnly '/usr/include', '/usr/include/x86_64-linux-gnu'
                }
            }
        }
        if (Target.shouldDefineTarget(project, Target.ALL_MACOS_HOSTED)) {
            def configureFrameworks = { target ->
                target.binaries {
                    framework {
                        baseName = "shared"
                        export project(':reaktive')
                        transitiveExport = true
                    }
                }
            }
            if (Target.shouldDefineTarget(project, Target.IOS)) {
                configureFrameworks(iosArm32)
                configureFrameworks(iosArm64)
                configureFrameworks(iosX64)
                configureFrameworks(iosSimulatorArm64)
            }
            if (Target.shouldDefineTarget(project, Target.MACOS)) {
                configureFrameworks(macosX64)
                configureFrameworks(macosArm64)
            }
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                api project(':reaktive')
                implementation project(':reaktive-annotations')
                implementation project(':utils')
            }
        }
    }
}
