plugins {
    id 'org.jetbrains.kotlin.multiplatform'
}

apply from: '../environment.gradle'

group = 'com.badoo.reaktive'
version = '0.0.1'

kotlin {
    targets {
        fromPreset(presets.jvm, 'jvm')
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-common'
            }
        }

        commonTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test'
                implementation "org.jetbrains.kotlin:kotlin-test-junit"
                implementation 'org.jetbrains.kotlin:kotlin-test-common'
                implementation 'org.jetbrains.kotlin:kotlin-test-annotations-common'
            }
        }

        jvmCommon {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib'
                dependsOn commonMain
            }
        }

        jvmMain {
            dependsOn jvmCommon
        }
    }
}

if (currentEnvironment().android) {

    apply plugin: 'com.android.library'

    android {
        compileSdkVersion 28
        defaultConfig {
            minSdkVersion 15
        }
    }

    kotlin {
        targets {
            fromPreset(presets.android, 'android')
        }
        sourceSets {
            androidMain {
                dependsOn jvmCommon
            }
        }
    }
}

if (currentEnvironment().osx) {

    kotlin {
        targets {
            presets.each { println(it.name) }
            fromPreset(presets.iosArm64, 'ios64') {
                compilations.main.outputKinds('FRAMEWORK')
            }
            fromPreset(presets.iosArm32, 'ios32') {
                compilations.main.outputKinds('FRAMEWORK')
            }
            fromPreset(presets.iosX64, 'iosSim') {
                compilations.main.outputKinds('FRAMEWORK')
            }
        }
        sourceSets {
            iosCommon.dependsOn commonMain
            iosCommonTest.dependsOn commonMain

            ios32Main.dependsOn iosCommon
            ios32Test.dependsOn iosCommonTest

            ios64Main.dependsOn iosCommon
            ios64Test.dependsOn iosCommonTest

            iosSimMain.dependsOn iosCommon
            iosSimTest.dependsOn iosCommonTest
        }
    }
}
