plugins {
    id 'org.jetbrains.kotlin.multiplatform'
}

apply from: '../environment.gradle'

group = 'com.badoo.reaktive'
version = '0.0.1'

kotlin {
    sourceSets {
        commonMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-common'
            }
        }

        commonTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test'
                implementation "org.jetbrains.kotlin:kotlin-test-junit"
                implementation 'org.jetbrains.kotlin:kotlin-test-common'
                implementation 'org.jetbrains.kotlin:kotlin-test-annotations-common'
            }
        }
    }
}

if (currentEnvironment().includeJvmCommon) {
    kotlin {
        targets {
            fromPreset(presets.jvm, 'jvm')
        }
        sourceSets {
            jvmCommon {
                dependencies {
                    implementation 'org.jetbrains.kotlin:kotlin-stdlib'
                }
            }
        }
    }
}

if (currentEnvironment().includeJvm) {
    kotlin {
        targets {
            fromPreset(presets.jvm, 'jvm')
        }
        sourceSets {
            jvmMain {
                dependsOn jvmCommon
            }
        }
    }
}

if (currentEnvironment().includeAndroid) {
    apply plugin: 'com.android.library'

    android {
        compileSdkVersion 28
        defaultConfig {
            minSdkVersion 15
        }
    }

    kotlin {
        targets {
            fromPreset(presets.android, 'android')
        }

        sourceSets {
            androidMain {
                dependsOn jvmCommon
            }
        }
    }
}
