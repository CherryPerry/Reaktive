buildscript {
    ext.reaktive_version = '1.0.0-rc1'
    ext.reaktive_group_id = 'com.badoo.reaktive'
}

apply from: 'gradle/versions.gradle.kts'

allprojects {
    repositories {
        google()
        jcenter()
    }

    configurations.all {
        resolutionStrategy {
            force 'com.android.support:appcompat-v7:28.0.0'
            force 'com.android.support.constraint:constraint-layout:1.1.3'
            force 'io.reactivex.rxjava2:rxjava:2.2.7'
            force 'io.reactivex.rxjava2:rxandroid:2.1.1'
        }
    }
}

void setupMultiplatformLibrary(Project project) {
    project.apply plugin: 'org.jetbrains.kotlin.multiplatform'
    project.apply plugin: 'maven-publish'

    project.group = reaktive_group_id
    project.version = reaktive_version

    project.kotlin {
        sourceSets {
            commonMain {
                dependencies {
                    implementation kotlin('stdlib-common')
                }
            }

            commonTest {
                dependencies {
                    implementation kotlin('test-common')
                    implementation kotlin('test-annotations-common')
                }
            }
        }
    }
}

void setupAllTargetsWithDefaultSourceSets(Project project) {
    final target = Target.currentTarget()

    if (target.isCommon() || target.isMeta()) {
        setupCommonTargets(project)

        project.kotlin {
            sourceSets {
                jvmMain.dependsOn commonMain
                jvmTest.dependsOn commonTest

                androidMain.dependsOn jvmMain
                androidTest.dependsOn jvmTest

                jsMain.dependsOn commonMain
                jsTest.dependsOn commonTest

                linuxX64Main.dependsOn commonMain
                linuxX64Test.dependsOn commonTest

                linuxArm32HfpMain.dependsOn commonMain
                linuxArm32HfpTest.dependsOn commonTest
            }
        }
    }

    if (target.isIos() || target.isMeta()) {
        setupIosTargets(project)

        project.kotlin {
            sourceSets {
                iosCommonMain.dependsOn commonMain
                iosCommonTest.dependsOn commonTest

                ios32Main.dependsOn iosCommonMain
                ios32Test.dependsOn iosCommonTest

                ios64Main.dependsOn iosCommonMain
                ios64Test.dependsOn iosCommonTest

                iosSimMain.dependsOn iosCommonMain
                iosSimTest.dependsOn iosCommonTest
            }
        }
    }
}

void setupAndroidTarget(Project project) {
    project.apply plugin: 'com.android.library'

    project.android {
        buildToolsVersion '28.0.3'
        compileSdkVersion 28

        defaultConfig {
            minSdkVersion 1
        }
    }

    project.kotlin {
        targets.fromPreset(presets.android, 'android')

        sourceSets {
            androidMain {
                dependencies {
                    implementation kotlin('stdlib')
                }
            }

            androidTest {
                dependencies {
                    implementation kotlin('test-junit')
                }
            }
        }

        android {
            publishLibraryVariants('release', 'debug')
        }
    }
}

void setupJvmTarget(Project project) {
    project.kotlin {
        targets.fromPreset(presets.jvm, 'jvm')

        sourceSets {
            jvmMain {
                dependencies {
                    implementation kotlin('stdlib')
                }
            }

            jvmTest {
                dependencies {
                    implementation kotlin('test-junit')
                }
            }
        }
    }
}


void setupJsTarget(Project project) {
    project.apply plugin: JsPlugin
}

void setupLinuxTargets(Project project) {
    project.kotlin {
        targets {
            fromPreset(presets.linuxX64, 'linuxX64')
            fromPreset(presets.linuxArm32Hfp, 'linuxArm32Hfp')
        }
    }
}

void setupCommonTargets(Project project) {
    setupAndroidTarget(project)
    setupJvmTarget(project)
    setupJsTarget(project)
    setupLinuxTargets(project)
}

void setupIosTargets(Project project) {
    project.apply plugin: IosPlugin
}
